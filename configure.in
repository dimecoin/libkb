dnl Process this file with autoconf to produce a configure script.

#
# Copyright (C) 1996-1998 Markus F.X.J. Oberhumer
#


# /***********************************************************************
# // Initialization
# ************************************************************************/

AC_INIT(src/kbhandle.c)
AC_PREREQ(2.12)
AC_CONFIG_AUX_DIR(acconfig)
AC_PREFIX_DEFAULT(/usr/local)
AM_INIT_AUTOMAKE(libkb,1.01)
AM_CONFIG_HEADER(config.h:config.hin)
AC_CANONICAL_HOST

AM_MAINTAINER_MODE
AM_CONDITIONAL(MAINTAINER, test x$USE_MAINTAINER_MODE = xyes)

mfx_PROG_MAKE_GNU(mfx_GMAKE)
AM_CONDITIONAL(GMAKE, test x$mfx_GMAKE = xyes)

mfx_enable_static="$enable_static"
mfx_enable_shared="$enable_shared"
AM_ENABLE_STATIC
AM_ENABLE_SHARED

AC_ARG_ENABLE(debug,
[  --enable-debug          build a debug version [default=no]],
[test "$enableval" = yes && enable_debug=yes])
test "$enable_debug" = yes || enable_debug=no


# /***********************************************************************
# // Prepare compiler flags
# ************************************************************************/

CPPFLAGS="$CPPFLAGS"
CFLAGS="$CFLAGS"
CFLAGS_O=""
CFLAGS_W=""
EXTRA_CFLAGS=""
AC_SUBST(CFLAGS_O)dnl
AC_SUBST(CFLAGS_W)dnl
AC_SUBST(EXTRA_CFLAGS)dnl


# /***********************************************************************
# // Checks for compiler
# ************************************************************************/

AC_PROG_CC
AM_PROG_CC_STDC
AC_PROG_CPP
AC_PROG_CC_C_O
AM_EXEEXT

dnl gcc -pipe makes troubles on some architectures (e.g. cygwin32)
if test "$USE_MAINTAINER_MODE" = yes; then
mfx_PROG_GCC_PIPE
fi

if test "$enable_debug" = yes; then
  CPPFLAGS="$CPPFLAGS -DKB_DEBUG=4"
else
  CPPFLAGS="$CPPFLAGS -DKB_DEBUG=1"
fi
AC_MSG_CHECKING([whether to build a debug version])
AC_MSG_RESULT($enable_debug)


# /***********************************************************************
# // Initialize libtool
# ************************************************************************/

dnl maintainer defaults to --disable-shared
if test "$USE_MAINTAINER_MODE" = yes; then
  test -z "$mfx_enable_shared" && enable_shared=no
fi

AM_PROG_LIBTOOL

libkb='$(top_builddir)/src/libkb.la'
AC_SUBST(libkb)


# /***********************************************************************
# // Checks for other programs
# ************************************************************************/

AM_PROG_INSTALL


# /***********************************************************************
# // Checks for SEAL audio library
# ************************************************************************/

seal_LIBS=""

AC_CHECK_HEADERS(audio.h)

if test "$ac_cv_header_audio_h" = yes; then
  save_LIBS="$LIBS"
  LIBS=""
  AC_CHECK_LIB(audio,AInitialize)

  if test "$ac_cv_lib_audio_AInitialize" = yes; then
     AC_DEFINE_UNQUOTED(USE_SEAL)
  fi

  seal_LIBS="$LIBS"
  LIBS="$save_LIBS"
fi


# /***********************************************************************
# // Checks for libraries
# ************************************************************************/

save_LIBS="$LIBS"
AC_CHECK_LIB(ncurses,init_color)
if test "$ac_cv_lib_ncurses_init_color" = no; then
  AC_CHECK_LIB(curses,initscr)
fi
kbtst_LIBS="$LIBS"
LIBS="$save_LIBS"
AC_SUBST(kbtst_LIBS)dnl

save_LIBS="$LIBS"
AC_CHECK_LIB(m,sin)
AC_CHECK_LIB(vga,vga_setmode)
AC_CHECK_LIB(vgagl,gl_setcontext)
dnl AC_CHECK_LIB(plush,plRenderBegin)
tube_LIBS="$seal_LIBS $LIBS"
LIBS="$save_LIBS"
AC_SUBST(tube_LIBS)dnl


# /***********************************************************************
# // Checks for system header files
# ************************************************************************/

AC_HEADER_STDC
AC_HEADER_TIME
AC_CHECK_HEADERS(assert.h ctype.h limits.h stddef.h time.h unistd.h)


# /***********************************************************************
# // Checks for library header files
# ************************************************************************/

AC_CHECK_HEADERS(curses.h ncurses.h)
AC_CHECK_HEADERS(vga.h vgagl.h vgakeyboard.h vgamouse.h)
AC_CHECK_HEADERS(plush.h)


# /***********************************************************************
# // Checks for typedefs and structures
# ************************************************************************/


# /***********************************************************************
# // Checks for compiler characteristics
# ************************************************************************/

AC_C_CONST


# /***********************************************************************
# // Checks for library functions
# ************************************************************************/

AC_CHECK_FUNCS(memcmp memcpy memmove memset)
mfx_FUNC_MEMCMP


# /***********************************************************************
# // Checks for system services
# ************************************************************************/


# /***********************************************************************
# // Customization
# ************************************************************************/

mfx_CUSTOMIZE

CPPFLAGS="$CPPFLAGS -DLIBKB_HAVE_CONFIG_H"

if test "$USE_MAINTAINER_MODE" = yes; then
  CPPFLAGS="$CPPFLAGS -DMAINT"
  test "$GCC" = yes && EXTRA_CFLAGS="$EXTRA_CFLAGS -Werror"
fi


# /***********************************************************************
# // Write output files
# ************************************************************************/

AC_OUTPUT(Makefile examples/Makefile include/Makefile src/Makefile util/Makefile)

